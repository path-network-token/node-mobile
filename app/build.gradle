import org.ajoberstar.grgit.Grgit

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: 'com.github.ben-manes.versions'

final def git = Grgit.open(currentDir: projectDir)
final def shortenedCommitHash = git.head().getAbbreviatedId()
final def commitsOnBranchCount = git.log(includes: ['HEAD']).size()

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "network.path.mobilenode"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode commitsOnBranchCount
        versionName "1.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'WEBSOCKET_SERVER_URL', '"ws://jobs-api.dev.udpflood.net/ws"'
        buildConfigField 'String', 'ICANHAZIP_BASE_URL', '"https://icanhazip.com/"'
        buildConfigField 'String', 'IPTOASN_BASE_URL', '"https://api.iptoasn.com/"'
    }

    testOptions {
        junitPlatform {
            unitTests {
                applyDefaultTestOptions true
            }
        }
    }

    signingConfigs {
        release {
            storeFile file(keystorePath)
            storePassword keystorePassword
            keyAlias keyAliasName
            keyPassword keyAliasPassword
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'WEBSOCKET_SERVER_URL', '"ws://jobs-api.path.network/ws"'
        }
        debug {
            ext.enableCrashlytics = false
            versionNameSuffix "$shortenedCommitHash.${commitsOnBranchCount}.debug"
        }
        mock {
            ext.enableCrashlytics = false
            initWith debug
            buildConfigField 'String', 'WEBSOCKET_SERVER_URL', '"ws://yehq4vtxw3nu.serveo.net"'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

dependencies {

    //Communication
    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"
    implementation "com.github.tinder.scarlet:scarlet:$versions.scarlet"
    implementation "com.github.tinder.scarlet:scarlet-websocket-okhttp:$versions.scarlet"
    implementation "com.github.tinder.scarlet:scarlet-message-adapter-gson:$versions.scarlet"
    implementation "com.github.tinder.scarlet:scarlet-stream-adapter-coroutines:$versions.scarlet"
    implementation "pl.droidsonroids:tracepath-android:$versions.tracepath"
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-scalars:$versions.retrofit"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-experimental-adapter:$versions.retrofitCoroutines"

    //Jetpack
    implementation "androidx.appcompat:appcompat:$versions.androidX"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintLayout"
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle"
    implementation "com.google.android.material:material:$versions.materialComponents"
    implementation "android.arch.navigation:navigation-fragment:$versions.navigation"
    kapt "androidx.lifecycle:lifecycle-compiler:$versions.lifecycle"

    //Ktx
    implementation "androidx.core:core-ktx:$versions.androidKtx"
    implementation "androidx.fragment:fragment-ktx:$versions.androidKtx"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"

    // Testing
    testImplementation "org.junit.jupiter:junit-jupiter-api:$versions.junit"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$versions.junit"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$versions.junit"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$versions.junit"
    testImplementation "junit:junit:$versions.oldJunit"
    testImplementation "org.assertj:assertj-core:$versions.assertJ"
    testImplementation "org.mockito:mockito-core:$versions.mockito"
    testImplementation "org.mockito:mockito-inline:$versions.mockito"
    testImplementation "com.nhaarman:mockito-kotlin:$versions.mockitoKotlin"
    testImplementation "androidx.arch.core:core-testing:$versions.lifecycle"

    //Kotlin
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$versions.coroutines"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"

    //DI
    implementation "org.koin:koin-core:$versions.koin"
    implementation "org.koin:koin-android:$versions.koin"
    implementation "org.koin:koin-androidx-viewmodel:$versions.koin"
    implementation "org.koin:koin-androidx-scope:$versions.koin"
    implementation "org.koin:koin-android-architecture:$versions.koinArchitecture"

    //GSON
    implementation "com.google.code.gson:gson:$versions.gson"

    //Location
    implementation "com.google.android.gms:play-services-location:$versions.location"

    //Logging
    implementation "com.jakewharton.timber:timber:$versions.timber"

    //Crash reporting
    implementation("com.crashlytics.sdk.android:crashlytics:$versions.crashlytics") {
        transitive = true
    }
}
