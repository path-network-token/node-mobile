import org.ajoberstar.grgit.Grgit

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

final def git = Grgit.open(currentDir: projectDir)
final def shortenedCommitHash = git.head().getAbbreviatedId()
final def commitsOnBranchCount = git.log(includes: ['HEAD']).size()

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "network.path.mobilenode"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode commitsOnBranchCount
        versionName "1.0.1"
    }

    signingConfigs {
        release {
            storeFile file(keystorePath)
            storePassword keystorePassword
            keyAlias keyAliasName
            keyPassword keyAliasPassword
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'WEBSOCKET_SERVER_URL', '"ws://jobs-api.path.network/ws"'
        }
        debug {
            versionNameSuffix "$shortenedCommitHash.${commitsOnBranchCount}.debug"
            buildConfigField 'String', 'WEBSOCKET_SERVER_URL', '"ws://jobs-api.dev.udpflood.net/ws"'
        }
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.0-rc02'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.google.android.gms:play-services-location:15.0.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.25.3'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "com.github.tinder.scarlet:scarlet:$scarlet_version"
    implementation "com.github.tinder.scarlet:scarlet-websocket-okhttp:$scarlet_version"
    implementation "com.github.tinder.scarlet:scarlet-message-adapter-gson:$scarlet_version"
    implementation "com.github.tinder.scarlet:scarlet-stream-adapter-coroutines:$scarlet_version"
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }
}