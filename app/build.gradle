import org.ajoberstar.grgit.Grgit

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: 'com.github.ben-manes.versions'

final def git = Grgit.open(currentDir: projectDir)
final def shortenedCommitHash = git.head().getAbbreviatedId()
final def commitsOnBranchCount = git.log(includes: ['HEAD']).size()

final def ver = rootProject.ext.versions

final def properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion ver.compileSdk
    defaultConfig {
        applicationId "network.path.mobilenode"
        minSdkVersion ver.minSdk
        targetSdkVersion ver.targetSdk
        versionCode ver.appVersionCode
        versionName ver.appVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [applicationLabel: "Path Mobile"]
    }

    applicationVariants.all { variant ->
        if (variant.buildType.isDebuggable()) {
            variant.outputs.each { output ->
                output.versionCodeOverride = commitsOnBranchCount
            }
        }
    }

    testOptions {
        junitPlatform {
            unitTests {
                applyDefaultTestOptions true
            }
        }
    }

    signingConfigs {
        release {
            storeFile file(properties.getProperty('keystore.path'))
            storePassword properties.getProperty('keystore.password')
            keyAlias properties.getProperty('keyalias.name')
            keyPassword properties.getProperty('keyalias.password')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            ext.enableCrashlytics = false
            versionNameSuffix "$shortenedCommitHash.${commitsOnBranchCount}.debug"
            applicationIdSuffix = ".debug"
            manifestPlaceholders = [applicationLabel: "Path Mobile Dev"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

//    lintOptions {
//        checkReleaseBuilds false
//        // Or, if you prefer, you can continue to check for errors in release builds,
//        // but continue the build even when errors are found:
//        abortOnError false
//    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "network.path.mobilenode:library:$versions.pathSdk"

    // AndroidX
    implementation "androidx.appcompat:appcompat:$versions.androidX"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintLayout"
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle"
    implementation "com.google.android.material:material:$versions.materialComponents"
    implementation "android.arch.navigation:navigation-fragment:$versions.navigation"
    kapt "androidx.lifecycle:lifecycle-compiler:$versions.lifecycle"

    // Ktx
    implementation "androidx.core:core-ktx:$versions.androidCoreKtx"
    implementation "androidx.fragment:fragment-ktx:$versions.androidFragmentKtx"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"

    // DI
    implementation "org.koin:koin-core:$versions.koin"
    implementation "org.koin:koin-android:$versions.koin"
    implementation "org.koin:koin-androidx-viewmodel:$versions.koin"
    implementation "org.koin:koin-androidx-scope:$versions.koin"
    implementation "org.koin:koin-android-architecture:$versions.koinArchitecture"

    // Web3j (I list okhttp dependencies explicitly to avoid version conflicts because of web3j)
    implementation "org.web3j:core:$versions.web3j"
    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"

    // Misc
    implementation "com.jakewharton.timber:timber:$versions.timber"
    implementation "org.jsoup:jsoup:$versions.jsoup"

    // Crash reporting
    implementation("com.crashlytics.sdk.android:crashlytics:$versions.crashlytics") {
        transitive = true
    }

    // Testing
    testImplementation "org.junit.jupiter:junit-jupiter-api:$versions.junit"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$versions.junit"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$versions.junit"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$versions.junit"
    testImplementation "junit:junit:$versions.oldJunit"
    testImplementation "org.assertj:assertj-core:$versions.assertJ"
    testImplementation "org.mockito:mockito-core:$versions.mockito"
    testImplementation "org.mockito:mockito-inline:$versions.mockito"
    testImplementation "com.nhaarman:mockito-kotlin:$versions.mockitoKotlin"
    testImplementation "androidx.arch.core:core-testing:$versions.lifecycle"
}
